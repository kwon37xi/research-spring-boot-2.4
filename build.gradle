plugins {
	id 'org.springframework.boot' version '2.4.1' apply false
	id 'io.spring.dependency-management' version '1.0.10.RELEASE' apply false
}
ext {
    springCloudVersion = "2020.0.0"
}
repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'kr.pe.kwonnam.research-springboot-2.4'
    version = '0.0.1-SNAPSHOT'

    sourceCompatibility = '11'
    targetCompatibility = '11'

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        implementation 'org.slf4j:jcl-over-slf4j'
        implementation 'org.slf4j:log4j-over-slf4j'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

        developmentOnly 'org.springframework.boot:spring-boot-devtools'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging' // replaced with jcl-over-slf4j
        all*.exclude group: 'log4j', module: 'log4j' // replaced with log4j-over-slf4j
        all*.exclude group: 'org.apache.logging.log4j' // replaced with log4j-over-slf4j
        all*.exclude module: 'slf4j-log4j12'

        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    test {
        useJUnitPlatform()
    }
}
